
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.jfrog.bintray' version "1.8.4"
    id 'maven-publish'
}

repositories {
	mavenCentral()
    jcenter()
}

group = 'org.ygl'
version = '0.0.1'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.ygl'
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven-libs'
        name = 'kx-animation'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/ylegall/kx-animation.git'
        version {
            name = "${project.version}"
            desc = 'kotlin animation library'
            released  = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.61'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'io.mockk:mockk:1.9.3'
}

